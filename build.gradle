plugins {
    id 'org.springframework.boot' version '2.7.15'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'java'
}


allprojects {

    group = 'camp.nextstep.edu'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.flywaydb:flyway-core'
        runtimeOnly 'com.h2database:h2'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
    }

    tasks.named('bootJar') {
        enabled = false;
    }

    tasks.named('jar') {
        enabled = true;
    }
}

project(':kitchenpos-order') {
    dependencies {
        implementation project(':kitchenpos-table')
        implementation project(':kitchenpos-table-group')
        implementation project(':kitchenpos-menu')
    }
}

project(':kitchenpos-table-group') {
    dependencies {
        implementation project(':kitchenpos-table')
    }
}

project(':kitchenpos-menu') {
    dependencies {
        implementation project(':kitchenpos-product')
        implementation project(':kitchenpos-menu-group')
    }
}

project(':kitchenpos-app') {
    tasks.named('bootJar') {
        enabled = true;
    }
    dependencies {
        implementation project(':kitchenpos-order')
        implementation project(':kitchenpos-product')
        implementation project(':kitchenpos-menu')
        implementation project(':kitchenpos-menu-group')
        implementation project(':kitchenpos-table')
        implementation project(':kitchenpos-table-group')
    }
}

